TODO :

   parsear errores de link (ver ejemplos al final)
   los errores de link NO apuntan a ninguna línea de código => otra lista
   checkear antes de editar que el fichero existe


#javaj#

   <layout of layJavaErrorsClickAndGo>
      EVA, 10, 10, 5, 5

      , X       , X
    X , o2Console, -
      , lErrors, lWarnings
    X , iErrors, iWarnings

#listix#


  <main0>
      VAR=, o2Console fileName, @<:lsx tmp>

  <javaErrorsClickAndGo::clear>
      MSG, o2Console clear
      MSG, o2Console save


  <FILENAME_LINE_PATT> //([^:]*):([^:]*):
  <FILENAME_LINE_PATT_mingw> //([^:]*):([^:]*):[^:]*:

  <javaErrorsClickAndGo::parseErrors>
      MSG, o2Console save

      PARSONS, FILE2DB, @<o2Console fileName>
             , CLEAN, 1
             , TABLE, msgErr
             ,      , fileName, line, //@<FILENAME_LINE_PATT_mingw> error:
             , TABLE, msgWarn
             ,      , fileName, line, //@<FILENAME_LINE_PATT_mingw> warning:

             , TABLE, msgErr
             ,      , fileName, line, //@<FILENAME_LINE_PATT> cannot resolve symbol

             , TABLE, msgErr
             ,      , fileName, line, //@<FILENAME_LINE_PATT> error:
             , TABLE, msgErr
             ,      , fileName, line, //\d*\. ERROR in (.*) \(at line (\d*)\)

             , TABLE, msgWarn
             ,      , fileName, line, //@<FILENAME_LINE_PATT> warning:
             , TABLE, msgWarn
             ,      , fileName, line, //\d*\. WARNING in (.*) \(at line (\d*)\)

             <! .... If not "error" and not "warning" then it is an error (javac 1.4 has no warnings)

             , TABLE, msgErr
             ,      , fileName, line, //@<FILENAME_LINE_PATT> .*

      -->, iErrors data!, sqlSelect, //SELECT fileName, line, lineNr+4 AS lineAbs FROM msgErr
      -->, iWarnings data!, sqlSelect, //SELECT fileName, line, lineNr+4 AS lineAbs FROM msgWarn

   <gotoline(file line)>
      CHECK, VAR, launch edit(file line), LISTIX, run StdEE.edit(file line), @<p1>, @<p2>
      LSX, launch edit(file line), @<p1>, @<p2>

   <-- iErrors>
      CHECK, VAR, iErrors selected.fileName
      MSG, o2Console gotoLine, @<iErrors selected.lineAbs>
      LSX, gotoline(file line), @<iErrors selected.fileName>, @<iErrors selected.line>

   <-- iWarnings>
      CHECK, VAR, iWarnings selected.fileName
      MSG, o2Console gotoLine, @<iWarnings selected.lineAbs>
      LSX, gotoline(file line), @<iWarnings selected.fileName>, @<iWarnings selected.line>

#**#
