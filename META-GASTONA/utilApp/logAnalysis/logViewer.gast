#gastona#

   <fusion>
      META-GASTONA/utilApp/logAnalysis/reportListixFlow.gasti
      META-GASTONA/utilApp/std/ExportBasicDialog.gasti


#javaj#

   <frames>
      Fmain, Analison

   <layout of Fmain>
      EVA, 10, 10, 6, 6

      ,              , X
      , bLoadLastLog ,
    X , aTree        , sMainView
      ,  +           , layExportBasic
      , oConson, -
      , bRun         , -

#data#

   <db> logDatabase.db

   <aTree>
      node

      arces
      logs/Errors
      logs/stackCalls
      listix/flow formats
      mensaka/agents
      mensaka/messages
      mensaka/lost messages
      mensaka/flow sent messages
      mensaka/flow detailed
      reports/listix flow text
      reports/listix flow html
      mix/mix1

#listix#

   <main0>
      DB CONFIG, DEFAULT, @<db>
      LSX, ensureDB

   <-- bLoadLastLog>
      LSX, createFreshLogDataBase

   <load logfile>
      //begin ;
      //.read logSession.sql
      //commit ;
      //

   <ensureDB>
      CHECK, FILE, @<db>, @<createFreshLogDataBase>

   <createFreshLogDataBase>
      GEN, :mem loadDB, load logfile
      DB,, DROP ALL TABLES
      DB,, EXECUTE
        , FROM FILE, :mem loadDB
      -->, sMainView data!, sqlSelect, //SELECT * FROM logMessages

   <-- aTree>
      IN CASE, @<aTree selectedPath>
             , logs/Errors   , -->, sMainView data!, sqlSelect, //SELECT * FROM logMessages WHERE clientId+0 != 0 AND level+0 < 10
             , logs/stackCalls, -->, sMainView data!, sqlSelect, //SELECT * FROM logStacksOnError

             , mensaka/agents             , -->, sMainView data!, sqlSelect, //SELECT agentId, componentName, objectToStr FROM logCustom_mensaka_sess_agents
             , mensaka/messages           , -->, sMainView data!, sqlSelect, //SELECT msgId, msgText FROM logCustom_mensaka_sess_msgs
             , mensaka/lost messages      , -->, sMainView data!, sqlSelect, //SELECT agentId, componentName, msgText FROM logCustom_mensaka_flow_lostMsgs LEFT JOIN logCustom_mensaka_sess_agents USING (agentId)
             , mensaka/flow sent messages , -->, sMainView data!, sqlSelect, //SELECT agentTx, componentName, msgId, msgText FROM (logCustom_mensaka_flow_sentMsgs LEFT JOIN logCustom_mensaka_sess_agents ON agentTx = agentId) LEFT JOIN logCustom_mensaka_sess_msgs USING (msgId) WHERE beginEnd='B'
             , mensaka/flow detailed      , -->, sMainView data!, sqlSelect, //SELECT agentTx, agentRx, componentName, msgId, msgText FROM (logCustom_mensaka_flow_sentMsgs LEFT JOIN logCustom_mensaka_sess_agents ON agentRx = agentId) LEFT JOIN logCustom_mensaka_sess_msgs USING (msgId) WHERE beginEnd='B'

             , listix/flow formats, -->, sMainView data!, sqlSelect, //SELECT * FROM logCustom_listix_flow;

             , mix/mix1, -->, sMainView data!, sqlSelect, @<SQL mix1>

      LISTIX, ExportBasic set (gridName dbName), sMainView, @<db>

   <-- aTree 2>
      IN CASE, @<aTree selectedPath>
             , arces , GAST, //META-GASTONA/utilApp/arces/arces.gast @<db>
             , reports/listix flow text,  @<show report listix_flow text>
             , reports/listix flow html,  @<show report listix_flow html>

   <show report listix_flow html>
      GEN, report_listix_flow.html, report listix_flow html
      BROWSE, report_listix_flow.html

   <show report listix_flow text>
      GEN, report_listix_flow.html, report listix_flow text
      CHECK, FILE, report_listix_flow.txt
      OPEN, report_listix_flow.txt

   <SQL mix1>
      // SELECT * FROM (
      //
      // SELECT
      //    milliStamp,
      //    "" AS mensaka,
      //    "" AS listix,
      //    "at "||context||" "||message AS errorTxt
      //    FROM logMessages
      //    WHERE level+0 < 10
      //
      // UNION
      //
      // SELECT
      //    milliStamp,
      //    "" AS mensaka,
      //    "<"||value||">" AS listix,
      //    "" AS errorTxt
      //    FROM  logCustom_listix_flow
      //
      //    WHERE context = 'flow' AND message ='format'
      //
      // UNION
      //
      // SELECT
      //    milliStamp ,
      //    "" AS mensaka,
      //    "    "||value AS listix,
      //    "" AS errorTxt
      //    FROM  logCustom_listix_flow
      //
      //    WHERE context = 'flow' AND message ='cmd'
      //
      //
      // UNION
      //
      // SELECT
      //    logCustom_mensaka_flow_sentMsgs.milliStamp AS milliStamp,
      //    "Message "||logCustom_mensaka_flow_sentMsgs.message||" "||msg.msgText AS mensaka,
      //    "" AS listix,
      //    "" AS errorTxt
      //
      //    FROM
      //
      //    logCustom_mensaka_flow_sentMsgs ,
      //    logCustom_mensaka_sess_msgs AS msg
      //
      //    WHERE
      //
      //    logCustom_mensaka_flow_sentMsgs.msgId == msg.msgId
      //
      // )
      // ORDER BY milliStamp+0
