#data#

#listix#

   <print schema>
      LSX, print schema (db), ""

   <print schema (db)>
      VAR=, theDB, @<p1>
      DB, @<theDB>, SCHEMA, theschema

      //=================== Schema (short form) from @<theDB>
      //
      //
      LSX, print short (schema), theschema
      //
      //
      //=================== Schema (long form) from @<theDB>
      //
      //
      LSX, print long (schema), theschema

   <print short (schema)>
      LOOP, EVA, theschema
          ,, //@<tabType> @<tableName>  (
          ,, BODY WHILE, id
          ,,           , LINK, ", "
          ,,           ,, @<columnName>
          ,, //)

   <print long (schema)>
      LOOP, EVA, theschema
         ,, //@<tabType> @<tableName> :
         ,, //
         ,, //@<see connections>
         ,, BODY WHILE, id
         ,,           ,, //   @<columnName>,
         ,, //
         ,, //


   <! acording to field names
   <!     __dbMore_connections (connName, sourceTable, sourceKey, targetTable, targetKey)
   <!


   <see connections>
      <! store "tableName" separately because now it will be masked by the next loop!
      VAR=, theTableName, @<tableName>

      <! loop of 1 to know if "__dbMore_connections" is there
      LOOP, EVA, theschema
          , FILTER, tableName, ==, __dbMore_connections
          , FILTER, columnName, ==, connName
          ,, LOOP, SQL, @<theDB>, //SELECT * FROM __dbMore_connections WHERE sourceTable = '@<theTableName>'
          ,,     ,, //   o--o Connection [@<connName>] to table [@<targetTable>] through (@<keys connection>)
      //
      //

   <keys connection>
      BODY WHILE, sourceTable
            , LINK, ", "
                ,, //@<sourceKey>
