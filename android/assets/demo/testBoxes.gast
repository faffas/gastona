#javaj#

   <frames> main, "Message boxes sample"

   <layout of main>

      EVA, 10, 10, 5, 5
      ---,
         , bToast
         , bInform
         , bWarning
         , bError
         , bQuestion
         , bSelection


#data#

   <some text>
      //The content of the message
      //can come from a separate variable
      //and can contain line breaks


   <! Data for the selection box
   <!

   <my choices>
      name, id, text

      Short wave, 1177, Some band
      FM        , 3122, No static at all
      Radio Gaga, 9231, All we hear is ...


#listix#

   <-- bToast>
      BOX, T, @<some text>

   <-- bInform>
      BOX, I, "This is how an information message looks like", "For your information"

   <-- bWarning>
      BOX, W, "This could be the content of the warning", "Here comes the warning"

   <-- bError>
      BOX, E, "And here you see what kind of error and its consequences etc..", "Ups, one error"

   <! Selection BOX
   <!
   <-- bSelection>
      BOX, S, my choices

   <! Reaction to a selection (message "my choice")
   <!
   <-- my choices>
      CHECK, VAR, my choices selected.name, BOX, I, //You haven't select anything!
      BOX, I, //You've selected @<my choices selected.name> which id is @<my choices selected.id> and the text "@<my choices selected.text>"

   <! Question BOX
   <!
   <-- bQuestion>
      BOX, Q, "Da ya think I'm Gastona ?", "This is my question"
         , LABELS  , "Yes, think so", "No", "Don't know"
         , MESSAGES, "answYes", "answNo", "answDontKnow"

   <! Reaction to the Question BOX
   <!
   <-- answYes>      BOX, T, //That's right!
   <-- answNo>       BOX, T, //That is not right!
   <-- answDontKnow> BOX, T, //I'll ask you again sometime ...
