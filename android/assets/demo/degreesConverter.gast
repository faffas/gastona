#javaj#

   <! GUI
   <! layout of the labels, edit fields and converter button
   <! for Celsius and Fahrenheit degrees
   <!

   <frames> main, Conversor

   <layout of main>
      EVA, 10, 10, 5, 5

         , X
         , lCelsius   ,
         , eCelsius   , bTo Fahrenheit
       22,
         , lFahrenheit,
         , eFahrenheit , bTo Celsius
       X , xSay, -

#listix#

   <! React on pressing button "To Fahrenheit"
   <!
   <-- bTo Fahrenheit>
      SET NUM, eFahrenheit, eCelsius * 9 / 5 + 32
      MSG, eFahrenheit data!
      @<check 86>

   <! React on pressing button "To Celsius"
   <!
   <-- bTo Celsius>
      SET NUM, eCelsius, (eFahrenheit - 32) * 5 / 9
      MSG, eCelsius data!
      @<check 86>

   <! Just add some logic if 86 Fahrenheit degrees is entered
   <!
   <check 86>
      CHECK, =, @<eFahrenheit>, 86, -->, xSay,, ""
      -->, xSay,, @<sausalito>

   <sausalito>
      // ...
      // eighty six degrees
      // take it as you please
      // with a certain kind of mellow ...

      